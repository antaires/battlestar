ArrayList <Bullet> bullets = new ArrayList <Bullet> ();
PVector player, playerSpeed;
float maxSpeed = 3;
 
void setup() {
  size(600, 600);
  player = new PVector(width/2, height/2);
  playerSpeed = new PVector();
  noCursor();
  noStroke();
  smooth();
}
 
void draw() {
  background(255);
 
  player.add(playerSpeed);
  fill(255, 0, 0);
  ellipse(player.x, player.y, 20, 20);
  fill(255);
  ellipse(player.x, player.y, 10, 10);
 
  PVector mouse = new PVector(mouseX, mouseY);
  fill(0);
  ellipse(mouse.x, mouse.y, 5, 5);
 
  if (frameCount%5==0 && mousePressed) {
    PVector dir = PVector.sub(mouse, player);
    dir.normalize();
    dir.mult(maxSpeed*3);
    Bullet b = new Bullet(player, dir);
    bullets.add(b);
  }
 
  for (Bullet b : bullets) {
    b.update();
    b.display();
  }
}
 
class Bullet extends PVector {
  PVector vel;
 
  Bullet(PVector loc, PVector vel) {
    super(loc.x, loc.y);
    this.vel = vel.get();
  }
 
  void update() {
    add(vel);
  }
 
  void display() {
    fill(0, 0, 255);
    ellipse(x, y, 3, 3);
  }
}
 
void keyPressed() {
  if (keyCode == UP)    { playerSpeed.y = -maxSpeed; }
  if (keyCode == DOWN)  { playerSpeed.y = maxSpeed;  }
  if (keyCode == LEFT)  { playerSpeed.x = -maxSpeed; }
  if (keyCode == RIGHT) { playerSpeed.x = maxSpeed;  }
}
 
void keyReleased() {
  if (keyCode == UP || keyCode == DOWN)    { playerSpeed.y = 0; }
  if (keyCode == LEFT || keyCode == RIGHT) { playerSpeed.x = 0; }
}
